#.udc to make the vtailVlm
# written by John Dannenhoffer

INTERFACE . ALL

# set a mark so that we can restore the stack back to
#    where it was when we started
MARK

# make sure we have the vtailOml and vtailHinges
UDPRIM    $/vtailOml
UDPRIM    $/vtailHinges

# get the bounding coordinates of the OML
RESTORE   vtailOml
SET       xmin     @xmin-1
SET       xmax     @xmax+1
SET       ymin     @ymin-1
SET       ymax     @ymax+1
SET       zmin     @zmin+0.0015
SET       zmax     @zmax-0.0015
STORE     .

# create a group of arfoil sections
# the order of the sections does not matter as they are sorted in the AIM
MARK
   # make root and tip
   PATBEG    icut  2
      SET       z  zmin+(zmax-zmin)*(icut-1)
      BOX       xmin  ymin  z  xmax-xmin  ymax-ymin  0
      RESTORE   vtailOml
      INTERSECT
         ATTRIBUTE _name         !$Vtail_+icut
      SELECT FACE 1
         ATTRIBUTE _color    $magenta
         ATTRIBUTE _bcolor   $magenta
   PATEND

   # beginning and end of each hinge
   PATBEG    ihinge  vtail:hinge.nrow*COMP:controls
      RESTORE   vtailHinge  ihinge
      SELECT    EDGE  1
      SET       zbeg     @zmin
      SET       zend     @zmax
      GETATTR   tagComp  $tagComp
      GETATTR   tagType  $tagType
      GETATTR   tagIndex $tagIndex
      GETATTR   deflect  $deflect
      GETATTR   xoverc1  $xoverc1
      GETATTR   xoverc2  $xoverc2
      STORE     .

      # create the section at the beginning
      BOX       xmin  ymin  zbeg  xmax-xmin  ymax-ymin  0
      RESTORE   vtailOml
      INTERSECT
         ATTRIBUTE _name                             !$VtailControl_+ihinge+$_1
         ATTRIBUTE !$vlmControl_VtailControl+tagIndex xoverc1
      SELECT FACE 1
         ATTRIBUTE _color                             $red
         ATTRIBUTE _bcolor                            $red

      # create the section at the end
      BOX       xmin  ymin  zend  xmax-xmin  ymax-ymin  0
      RESTORE   vtailOml
      INTERSECT
         ATTRIBUTE _name                             !$VtailControl_+ihinge+$_2
         ATTRIBUTE !$vlmControl_VtailControl+tagIndex xoverc2
      SELECT FACE 1
         ATTRIBUTE _color                             $blue
         ATTRIBUTE _bcolor                            $blue
   PATEND
GROUP

# store the group
STORE     vtailVlm

# make sure that we did not leave any new Bodys on the stack
STORE     ..

END
